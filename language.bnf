<query>         ::= <select> [<where>] [<order>] [<show>]
                  | <insert> <values> [<associate>]
                  | <update> <where> <set> <bothassociate> | <update> <where> <set> | <update> <where> <bothassociate>
                  | <delete> [<where>]
                  | <undelete> [<where>]
                  | <describe>
                  ;


; Query types
<select>        ::= "SELECT" <locationgraph> ;
<insert>        ::= "INSERT" <locationgraph> ;
<update>        ::= "UPDATE" <entityname> ;
<delete>        ::= "DELETE" <entityname> ;
<undelete>      ::= "UNDELETE" <entityname> ;
<describe>      ::= "DESCRIBE" <entityname> ;


; LOCATION GRAPH
<locationgraph> ::= <entityname>":[" <locationgraphi> "]" ;

<locationgraphi>::= <entityname>":[" <locationgraphi> "]" ["," <locationgraphi>]
                  | <entityname> ["," <locationgraphi>]
                  | <locationagg> ["," <locationgraphi>]
                  | <namespacecount> ["," <locationgraphi>]
                  ;

<locationagg>   ::= <aggfuncname>"(" <location> ")" <alias> ;
<namespacecount>::= <countfuncname>"(" <namespace> ")" <alias> ;

<alias>         ::= "AS" <entityname> ;


; WHERE
<where>         ::= WHERE <wheres> ;

<wheres>        ::= <wherecompare> ["AND" <wheres>] ;

<wherecompare>  ::= <namespace>":(" <compares> ")" ;

<compares>      ::= <compare> ["AND" <compares>];

<compare>       ::= <entityname> <comparison> <value>
                  | <entityname> "IN" <parameter>
                  | <hascompare> <comparison> <value>
                  | <locationagg> <comparison> <value>
                  | <namespacecount> <comparison> <value>
                  ;

<hascompare>    ::= "HAS(" <location> <comparison> <value> ")"
                  | "HAS(" <location> "IN" <parameter> ")"
                  ;


; VALUES
<values>        ::= VALUES <valuegraph> ;

<valuegraph>    ::= <entityname>":[" <valuegraphi> "]" ;

<valuegraphi>   ::= <entityname>":[" <valuegraphi> "]" ["," <valuegraphi>]
                  | <value> ["," <valuegraphi>]
                  ;


; SET
<set>           ::= SET <sets> ;

<sets>          ::= <colassign> ["," <sets>] ;

<entityassign>  ::= <entityname> <assign> <value> ;


// ASSOCIATE
<bothassociate> ::= <associate>
                  | <disassociate>
                  | <associate> <disassociate>
                  ;

<associate>     ::= "ASSOCIATE" <associates> ;

<disassociate>  ::= "DISASSOCIATE" <associates> ;

<associates>    ::= <colexpr> ["," <associates>] ;

<colexpr>       ::= <column> <comparison> <value> ;


; ORDER
<order>         ::= "ORDER" <orders> ;

<orders>        ::= <orderby> ["," <orders>] ;

<orderby>       ::= <namespace> "BY" <entityname> <orderoptions> ;

<orderoptions>  ::= "ASC" | "DESC" ;


; SHOW
<show>          ::= "SHOW" <shows> ;

<shows>         ::= <showrp> ["," <showrs>] ;

<showrs>        ::= <showr> ["," <showrs>] ;

<showrp>        ::= <showr> ["PAGE" <posinteger>] ;

<showr>         ::= <posinteger> <namespace> ;


; Columns, locations, and namespaces

<location>      ::= <namespace>":"<entityname> ;

<column>        ::= <entityname>":"<entityname> ;

<namespace>     ::= <entityname>["."<namespace>] ;


; Low level
<entityname>    ::= "`"<objectname>"`" | <objectname> ;

<value>         ::= <constant> | <parameter> ;

<parameter>     ::= "?"<alphanum> ;

<constant>      ::= <double> | <integer> | <string> | <boolean> ;


; Operators
<comparison>    ::= "==" | "!=" | "<" | "<=" | ">" | ">=" ;
<assign>        ::= "=" ;


; Function names
<aggfuncname>   ::= "SUM" | "AVERAGE" | "MEAN" | "MEDIAN" | "MIN" | "MAX" ;
<countfuncname> ::= "COUNT" ;


; Tokens
<objectname>    ::= ^([0-9a-zA-Z$_]+)(?=[\s`,.\)\]]) ;
<alphanum>      ::= ^([a-zA-Z0-9]+)(?=[\s`,.\)\]]) ;
<integer>       ::= ^([-]?\d+)(?=[\s`,.\)\]]) ;
<posinteger>    ::= ^([1-9]+[0-9]*)(?=[\s`,.\)\]]) ;
<double>        ::= ^([-]?[0-9]*\.[0-9]+)(?=[\s`,.\)\]]) ;
<string>        ::= ^(L?\"(\\.|[^\\"])*\")(?=[\s`,.\)\]]) ;
<boolean>       ::= "true" | "false" ;
